
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт 
	
	// ++ Исаев Д.А. Задача: диплом от 21.01.2026
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;	
	// -- Исаев Д.А. от 21.01.2026

КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт 
	
	// ++ Исаев Д.А. Задача: диплом от 21.01.2026
	// Акт об оказанных услугах
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОказанныхУслугах";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказанных услугах'");
	КомандаПечати.Порядок = 5; 		
	// -- Исаев Д.А. от 21.01.2026
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// ++ Исаев Д.А. Задача: диплом от 21.01.2026
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОбОказанныхУслугах"); 
	
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАктаОбОказанныхУслугах(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказанных услугах'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОбОказанныхУслугах";
	КонецЕсли; 	
	// -- Исаев Д.А. от 21.01.2026
		
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАктаОбОказанныхУслугах(МассивОбъектов, ОбъектыПечати) 
	// ++ Исаев Д.А. Задача: диплом от 21.01.2026
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктОбОказанныхУслугах"; 
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОбОказанныхУслугах"); 	
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц(); 
		КонецЕсли;
		
		ПервыйДокумент = Ложь;  
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиШапка(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиУслугиТЧ(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиПодвал(ДанныеДокументов, ТабличныйДокумент, Макет);
				
	КонецЦикла; 
	
	Возврат ТабличныйДокумент;		
	// -- Исаев Д.А. от 21.01.2026 
		
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	// ++ Исаев Д.А. Задача: диплом от 21.01.2026
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Номер КАК Номер,
	               |	РеализацияТоваровУслуг.Дата КАК Дата,
	               |	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	               |	РеализацияТоваровУслуг.Организация КАК Организация,
	               |	РеализацияТоваровУслуг.Договор КАК Договор,
	               |	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	               |	РеализацияТоваровУслуг.Услуги.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();	
	// -- Исаев Д.А. от 21.01.2026
	
КонецФункции

Процедура ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	// ++ Исаев Д.А. Задача: диплом от 21.01.2026
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	
	ОбластьЗаголовок.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер);	
	ОбластьЗаголовок.Параметры.Дата = Формат(ДанныеДокументов.Дата, "ДЛФ=DD");
		
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);	
	// -- Исаев Д.А. от 21.01.2026
	
КонецПроцедуры

Процедура ВывестиШапка(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	// ++ Исаев Д.А. Задача: диплом от 21.01.2026
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьШапка.Параметры.Контрагент = ДанныеДокументов.Контрагент;
	ОбластьШапка.Параметры.Организация = ДанныеДокументов.Организация;
	ОбластьШапка.Параметры.Договор = ДанныеДокументов.Договор;
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	// -- Исаев Д.А. от 21.01.2026
		
КонецПроцедуры

Процедура ВывестиУслугиТЧ(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	// ++ Исаев Д.А. Задача: диплом от 21.01.2026
	ОбластьШапкаТЧ = Макет.ПолучитьОбласть("УслугиШапкаТЧ");
	ОбластьСтрокаТЧ = Макет.ПолучитьОбласть("УслугиСтрокаТЧ");
		
	ТабличныйДокумент.Вывести(ОбластьШапкаТЧ);
	
	ВыборкаУслуги = ДанныеДокументов.Услуги.Выбрать();
	
	Пока ВыборкаУслуги.Следующий() Цикл 
		
		ОбластьСтрокаТЧ.Параметры.Заполнить(ВыборкаУслуги);
		
		ТабличныйДокумент.Вывести(ОбластьСтрокаТЧ);
		
	КонецЦикла;	
	// -- Исаев Д.А. от 21.01.2026
		
КонецПроцедуры 

Процедура ВывестиПодвал(ДанныеДокументов, ТабличныйДокумент, Макет)
	
    // ++ Исаев Д.А. Задача: диплом от 21.01.2026
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ОбластьПодвал.Параметры.ИтоговаяСумма = ДанныеДокументов.СуммаДокумента;
	ОбластьПодвал.Параметры.ИтоговаяСуммаПрописью = ЧислоПрописью(ДанныеДокументов.СуммаДокумента, ,"рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2");	
	
	ТабличныйДокумент.Вывести(ОбластьПодвал);	
	// -- Исаев Д.А. от 21.01.2026	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли