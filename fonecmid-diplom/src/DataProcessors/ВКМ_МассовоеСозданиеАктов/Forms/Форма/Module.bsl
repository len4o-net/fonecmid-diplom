
#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура Заполнить(Команда) 
	
	ДлительнаяОперация = ВычислитьДокументыРеализацииНаСервере();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатДлительнойОперации", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	
	
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Функция ВычислитьДокументыРеализацииНаСервере()
	
	ДокументыРеализации = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, 
	"Обработки.ВКМ_МассовоеСозданиеАктов.ПолучитьДокументыРеализации", Объект.Период.ДатаНачала, Объект.Период.ДатаОкончания); 
	
	Возврат ДокументыРеализации;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатДлительнойОперации(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТЧСписокДоговоров(Результат.АдресРезультата);
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьТЧСписокДоговоров(АдресРезультата)
	
	Объект.СписокДоговоров.Очистить();
	
	СтрокаJSON = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	УИДРеализаций = ПрочитатьЗначениеJSON(СтрокаJSON);
	
	Для Каждого Структура Из УИДРеализаций Цикл
		
		ДоговорКонтрагентовСсылка = Справочники.ДоговорыКонтрагентов.ПолучитьСсылку(Новый УникальныйИдентификатор(Структура.Договор));
		РеализацияСсылка = Документы.РеализацияТоваровУслуг.ПолучитьСсылку(Новый УникальныйИдентификатор(Структура.Реализация));
		
		НоваяСтрокаТЧ = Объект.СписокДоговоров.Добавить();
		НоваяСтрокаТЧ.Договор = ДоговорКонтрагентовСсылка;
		НоваяСтрокаТЧ.Реализация = РеализацияСсылка;		
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти